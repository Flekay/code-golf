#include <u16>




fn sub_with_flag(struct u16 self, struct u16 other, cell _carried) {
  copy other.n1 {
    if not self.n1 {
      _carried = true;
    }
    self.n1 -= 1;
  }
  copy other.n0 {
    if not self.n0 {
      if not self.n1 {
        _carried = true;
      }
      self.n1 -= 1;
    }
    self.n0 -= 1;
  }
}

fn is_zero(struct u16 self, cell _is_zero) {
  _is_zero = true;
  if self.n0 {
    _is_zero = false;
  }
  if self.n1 {
    _is_zero = false;
  }
}

fn divisible(struct u16 dividend, struct u16 divisor, cell _divisible) {
  struct u16 a; set(a, dividend);
  _divisible = true;
  cell dividing = true;
  while dividing {
    cell carried;
    sub_with_flag(a, divisor, carried);
    if carried {
      dividing = false;
      _divisible = false;
    }
    cell foo;
    is_zero(a, foo);
    if foo {
      dividing = false;
    }
  }
}




fn print_if_prime(struct u16 n) {
  // could use div_2
  struct u16 _n; set(_n, n);
  {cell _2 = 2; sub(_n, _2);}
  cell iterating = true;
  cell is_prime = true;
  struct u16 i; i.n0 = 2;
  // for i in 2..n
  while iterating {
// output" n:";print(n);output" (";debug(n);output")\n";
// output" i:";print(i);output" (";debug(i);output")\n";
// output" _n:";print(_n);output" (";debug(_n);output")\n";
// output 10;
    {cell d; divisible(n, i, d); if d {is_prime = false;}}
    
    inc(i);
    dec(_n);
    ne_zero(_n, iterating);
    if not is_prime {
      iterating = false;
    }
  }

  if is_prime {
    print(n);
    output 10;
  }
}




fn main() {
  output "2\n";
  struct u16 n; n.n0 = 3;
  drain 100 {
    drain 100 {
      print_if_prime(n);
      inc(n);
    }
  }
}

main();
